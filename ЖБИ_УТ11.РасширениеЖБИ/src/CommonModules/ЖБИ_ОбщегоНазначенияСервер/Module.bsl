
Функция РазницаВМесяцах(ДатаНач, ДатаКон) Экспорт 
	
    Лет1 = Год(ДатаНач);
    Месяцев1 = Месяц(ДатаНач);
    Если НачалоДня(ДатаНач) <> НачалоДня(КонецМесяца(ДатаНач)) Тогда
        Дней1 = День(ДатаНач);
    Иначе
        Дней1 = 30;//В каждом месяце ровно 30 дней
    КонецЕсли; 

    Лет2 = Год(ДатаКон);
    Месяцев2 = Месяц(ДатаКон);
    Если НачалоДня(ДатаКон) <> НачалоДня(КонецМесяца(ДатаКон)) Тогда
        Дней2 = День(ДатаКон);
    Иначе
        Дней2 = 30;//В каждом месяце ровно 30 дней
    КонецЕсли;     

    Лет = Лет2 - Лет1;
    Месяцев = Месяцев2 - Месяцев1;
    Дней = Дней2 - Дней1;
	
    Возврат Цел(Лет*12 + Месяцев + Дней/30);
	
КонецФункции

Функция МассивВТаблицуЗначений(ИсходныйМассив, Знач ИмяКолонки="") Экспорт
	
	Если ПустаяСтрока(ИмяКолонки) Тогда
		ИмяКолонки = "Колонка1";
	КонецЕсли;
	
	ИмяКолонки = СтрЗаменить(ИмяКолонки, " ", "_");
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить(ИмяКолонки);
	
	Если ЗначениеЗаполнено(ИсходныйМассив) Тогда
		Для Сч = 1 По ИсходныйМассив.Количество() Цикл
			Результат.Добавить();
		КонецЦикла;
		Результат.ЗагрузитьКолонку(ИсходныйМассив, ИмяКолонки);
	КонецЕсли;
	
	Возврат Результат;	

КонецФункции // МассивВТаблицуЗначений()

Функция ПолучитьПодписантаПоПраву(ДокументОбъект, Организация) Экспорт 
	
	ОтветственныйСотрудник = Справочники.ОтветственныеЛицаОрганизаций.ПустаяСсылка();
	
	ТипДокумента 		   = ТипЗнч(ДокументОбъект);
	Запрос 				   = Новый Запрос; 
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Наименование", Строка(ТипДокумента));
	Запрос.Текст 		   = "ВЫБРАТЬ ПЕРВЫЕ 1
							|	ЖБИ_ПраваНастройки.Значение КАК Значение
							|ИЗ
							|	РегистрСведений.ЖБИ_ПраваНастройки КАК ЖБИ_ПраваНастройки
							|ГДЕ
							|	ВЫРАЗИТЬ(ЖБИ_ПраваНастройки.Объект КАК Справочник.Организации) = &Организация 
							|	И ЖБИ_ПраваНастройки.ПравоНастройка.СлужебныйКод = 100 
							|	И ЖБИ_ПраваНастройки.ПравоНастройка.Наименование = &Наименование";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		ОтветственныйСотрудник = РезультатЗапроса.Значение;	
	КонецЕсли;
	
	Возврат ОтветственныйСотрудник; 
	
КонецФункции

Функция ПолучитьСтавкуНДСНаСервере(Номенклатура) Экспорт
	СтавкаНДС	= Номенклатура.СтавкаНДС;
	УчетНДСУП.СкорректироватьСтавкуНДС(СтавкаНДС, ТекущаяДатаСеанса());
	Возврат СтавкаНДС;
КонецФункции

Функция ПодписьПользователяФорматированныйДокумент() Экспорт
	
	ФорматированныйДокумент 		= Новый ФорматированныйДокумент;
	ТекущийПользователь 			= Пользователи.ТекущийПользователь();
	Запрос 							= Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Запрос.Текст					= "ВЫБРАТЬ ПЕРВЫЕ 1
				            		  |	Пользователи.Ссылка КАК Ссылка,
				            		  |	Пользователи.ЖБИ_ЭлектроннаяПодписьПользователя КАК ЖБИ_ЭлектроннаяПодписьПользователя
				            		  |ИЗ
				            		  |	Справочник.Пользователи КАК Пользователи
									  |ГДЕ
									  |	Пользователи.Ссылка = &Пользователь	
									  |	И НЕ Пользователи.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		ФорматированныйДокумент = РезультатЗапроса.ЖБИ_ЭлектроннаяПодписьПользователя.ТекстHTML.Получить();	
	КонецЕсли;
	
	Возврат ФорматированныйДокумент;
	
КонецФункции

Функция ЗаполнитьТекстПоУмолчанию() Экспорт
	
	СтруктураТема 	= Новый Структура("Тема,Текст","","");
	Запрос 			= Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				   |	ЖБИ_ШаблонПочтовыхСообщений.Наименование КАК Тема,
	               |	ЖБИ_ШаблонПочтовыхСообщений.ШаблонСообщения КАК Текст
	               |ИЗ
	               |	Справочник.ЖБИ_ШаблонПочтовыхСообщений КАК ЖБИ_ШаблонПочтовыхСообщений
				   |ГДЕ
				   |	НЕ ЖБИ_ШаблонПочтовыхСообщений.ПометкаУдаления
				   |	И ЖБИ_ШаблонПочтовыхСообщений.ПоУмолчанию";	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда 
		СтруктураТема.Тема = РезультатЗапроса.Тема;
		СтруктураТема.Текст = РезультатЗапроса.Текст;
	КонецЕсли;
	
	Возврат СтруктураТема;
	
КонецФункции

Функция ЗаполнитьВесТовара(Поставщик,НоменклатураПоставщика) Экспорт 
	
	Вес 	= 0;
	Запрос  = Новый Запрос;
	Запрос.УстановитьПараметр("НоменклатураПоставщика",НоменклатураПоставщика);
	Запрос.УстановитьПараметр("Поставщик",Поставщик);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
					|	ЖБИ_НоменклатураПоставщиков.НоменклатураПоставщика КАК НоменклатураПоставщика,
					|	ЖБИ_НоменклатураПоставщиков.Вес КАК Вес 
					|ИЗ
					|	РегистрСведений.ЖБИ_НоменклатураПоставщиков КАК ЖБИ_НоменклатураПоставщиков
					|ГДЕ
					|	ЖБИ_НоменклатураПоставщиков.Партнер = &Поставщик
					|	И ЖБИ_НоменклатураПоставщиков.НоменклатураПоставщика = &НоменклатураПоставщика";						
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		Вес = РезультатЗапроса.Вес;
	КонецЕсли;
	
	Возврат Вес;
	
КонецФункции

Функция УникальноеИмяФайла(ИмяФайла) Экспорт
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	Папка = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(Папка + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;

КонецФункции

Функция ПечатьСерверный(ИмяФормыПечати, ДокументСсылка, ДопПараметры = Неопределено) Экспорт
	
	МассивПечатныхФорм = Новый Массив;
	
	КоллекцияПечатныхФорм 	= Новый ТаблицаЗначений;
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяМакета");
	КоллекцияПечатныхФорм.Колонки.Добавить("ТабличныйДокумент");
	КоллекцияПечатныхФорм.Колонки.Добавить("СинонимМакета");
	КоллекцияПечатныхФорм.Колонки.Добавить("Картинка");	
	КоллекцияПечатныхФорм.Колонки.Добавить("ПолныйПутьКМакету");
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяФайлаПечатнойФормы");
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяВРЕГ");
	НоваяСтр 				= КоллекцияПечатныхФорм.Добавить();
	НоваяСтр.ИмяВРЕГ		= ВРЕГ(ИмяФормыПечати);
	НоваяСтр.ИмяМакета      = ИмяФормыПечати;
	
	ИмяДокумента 			= ДокументСсылка.Метаданные().Имя;
	ПараметрыПечати 		= Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Ложь);
	Если ДопПараметры <> Неопределено Тогда
		Если ДопПараметры.Свойство("ОтображатьФаксимиле") Тогда 
			ПараметрыПечати.Вставить("ОтображатьФаксимиле", Истина);
		КонецЕсли;
	КонецЕсли;
	МассивОбъектов			= Новый Массив;
	МассивОбъектов.Добавить(ДокументСсылка);
	
	Если СтрНайти(ИмяФормыПечати,"ЖБИ")>0 Тогда
		Документы[ИмяДокумента].ЖБИ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура, МассивПечатныхФорм);
	Иначе
		Документы[ИмяДокумента].Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура);
	КонецЕсли;
	
	Возврат МассивПечатныхФорм;
	
КонецФункции