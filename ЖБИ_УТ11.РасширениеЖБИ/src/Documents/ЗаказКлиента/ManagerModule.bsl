#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ОБЩЕГО_НАЗНАЧЕНИЯ

#Область РасширениеЖБИ

Функция ЗаполнитьНомерСпецификации() Экспорт
	
	НомерСпецификации  = 0;
	Запрос 			   = Новый Запрос;
	Запрос.Текст 	   = "ВЫБРАТЬ
	             	  |	ЕСТЬNULL(МАКСИМУМ(ЗаказКлиента.ЖБИ_НомерСпецификации),0) КАК НомерСпецификации
	             	  |ИЗ
	             	  |	Документ.ЗаказКлиента КАК ЗаказКлиента
	             	  |ГДЕ
	             	  |	НЕ ЗаказКлиента.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда 
		НомерСпецификации = РезультатЗапроса.НомерСпецификации + 1;	
	КонецЕсли;
	
	Если НомерСпецификации = 0 Тогда
		НомерСпецификации = 1;
	КонецЕсли;
	
	Возврат НомерСпецификации;
	
КонецФункции

#КонецОбласти

&Перед("ДобавитьКомандыПечати")
Процедура ЖБИ_ДобавитьКомандыПечати(КомандыПечати)
	
	// СчетНаОплату
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЖБИ_СчетНаОплату";
	КомандаПечати.Представление = НСтр("ru = 'ЖБИ - Счет на оплату'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЖБИ_СчетНаОплату";
	КомандаПечати.Представление = НСтр("ru = 'ЖБИ - Счет на оплату с факсимиле'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ОтображатьФаксимиле", Истина);
	
	// Спецификация
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЖБИ_Спецификация";
	КомандаПечати.Представление = НСтр("ru = 'ЖБИ - Спецификация'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

&Перед("Печать")
Процедура ЖБИ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Печатать = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЖБИ_СчетНаОплату");	
	Если Печатать Тогда
		СтруктураТипов 	= ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
		СсылкаЗаказ 	= МассивОбъектов[0];
		СтруктураДок	= Новый Структура("Номер",СсылкаЗаказ.Номер);
		НомерДокСтр 	= СтрЗаменить(ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(СтруктураДок, "Счет"),"от","");
		СинонимМакета 	= НомерДокСтр + СсылкаЗаказ.Партнер.Наименование;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЖБИ_СчетНаОплату",
		СинонимМакета,
		ЖБИСформироватьПечатнуюФормуСчетНаОплату(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
	Печатать = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЖБИ_Спецификация");	
	Если Печатать Тогда
		СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
		СинонимМакета = НСтр("ru = 'ЖБИ - Спецификация'");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЖБИ_Спецификация",
		СинонимМакета,
		ЖБИСформироватьПечатнуюФормуСпецификация(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ЖБИСформироватьПечатнуюФормуСчетНаОплату(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЧЕТНАОПЛАТУ";
	
	НомерТипаДокумента 	= 0;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыСчетаНаОплату(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		ЖБИ_ОбщийМодульПечать.ЗаполнитьТабличныйДокументСчетаНаОплату(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ЖБИСформироватьПечатнуюФормуСпецификация(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЧЕТНАОПЛАТУ";
	
	НомерТипаДокумента = 0;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыСчетаНаОплату(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		ЖБИ_ОбщийМодульПечать.ЗаполнитьТабличныйДокументСпецификация(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецЕсли
