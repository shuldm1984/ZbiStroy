

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ОБЩЕГО_НАЗНАЧЕНИЯ

#Область Печать

&НаСервере
Процедура СформироватьПечатныеФормыДоговорЗаявка(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм)
			
	Для Каждого НомСтр Из ОбъектПечати.ДокументыОтгрузки Цикл
		Если ТипЗнч(НомСтр.ДокументОтгрузки) <> Тип("ДокументСсылка.ЖБИ_ДоговорЗаявка") Тогда 
			Продолжить;
		КонецЕсли;
		Если НомСтр.ДокументОтгрузки.Статус <> Перечисления.ЖБИ_СтатусРасчетаСтавки.ВРаботе Тогда 
			Продолжить;
		КонецЕсли;
		Если НомСтр.ДокументОтгрузки.ПометкаУдаления Тогда 
			Продолжить;
		КонецЕсли;
		МассивОбъектов 	= Новый Массив;
		МассивОбъектов.Добавить(НомСтр.ДокументОтгрузки);
		Документы.ЖБИ_ДоговорЗаявка.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура, МассивПечатныхФорм);	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СформироватьПечатныеФормыДоверенность(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм)
	
	Для Каждого НомСтр Из ОбъектПечати.ДокументыОтгрузки Цикл
		Если ТипЗнч(НомСтр.ДокументОтгрузки) <> Тип("ДокументСсылка.ДоверенностьВыданная") Тогда 
			Продолжить;
		КонецЕсли;
		Если НомСтр.ДокументОтгрузки.Статус <> Перечисления.СтатусыДоверенностей.НеВыдана Тогда 
			Продолжить;
		КонецЕсли;
		Если НомСтр.ДокументОтгрузки.ПометкаУдаления Тогда 
			Продолжить;
		КонецЕсли;
		МассивОбъектов 	= Новый Массив;
		МассивОбъектов.Добавить(НомСтр.ДокументОтгрузки);
		Документы.ДоверенностьВыданная.ЖБИ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура);
		МассивПечатныхФорм.Добавить(КоллекцияПечатныхФорм[0].ТабличныйДокумент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПечатныеФормыТТН(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм)
	
	ПараметрыВывода 	= Новый Структура("ПараметрыОтправки",Новый Структура("Получатель,Тема",Неопределено,""));
	Для Каждого НомСтр Из ОбъектПечати.ДокументыОтгрузки Цикл
		Если ТипЗнч(НомСтр.ДокументОтгрузки) <> Тип("ДокументСсылка.ТранспортнаяНакладная") Тогда 
			Продолжить;
		КонецЕсли;
		Если НомСтр.ДокументОтгрузки.ПометкаУдаления Тогда 
			Продолжить;
		КонецЕсли;
		МассивОбъектов 	= Новый Массив;
		МассивОбъектов.Добавить(НомСтр.ДокументОтгрузки);
		Документы.ТранспортнаяНакладная.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура, МассивПечатныхФорм);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокПечатныхФорм()
	
	СтруктураПечатныхФорм = Новый Структура;	
	СтруктураПечатныхФорм.Вставить("ЖБИ_ДоговорЗаявка", "ЖБИ - Договор заявка");
	СтруктураПечатныхФорм.Вставить("ЖБИ_Доверенность", "ЖБИ - Доверенность");
	СтруктураПечатныхФорм.Вставить("ЖБИ_ТТН", "ЖБИ - ТТН");
	Возврат СтруктураПечатныхФорм;
	
КонецФункции

&НаКлиенте
Процедура Печать(Команда)
	 
	ТекДанные = ЖБИ_ОбщийМодульКлиентСервер.ПроверитьТекСтрокуНаДоступность(ЭтаФорма,"Список");
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	 
	ИмяФормыПечати 	= СтрЗаменить(Команда.Имя, "Печать_", "");
	ОбъектПечати 	= ТекДанные.Ссылка; 
	Если НЕ ЗначениеЗаполнено(ОбъектПечати) Тогда 
		Возврат;
	КонецЕсли;	

	МассивПечатныхФорм 	= ПечатьСерверный(ИмяФормыПечати,ОбъектПечати);
	Если ЗначениеЗаполнено(МассивПечатныхФорм) Тогда
		ЖБИ_ОбщийМодульКлиентСервер.СформироватьПечатнуюФорму(ЭтаФорма,МассивПечатныхФорм);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПечатьСерверный(ИмяФормыПечати, ОбъектПечати)
	
	МассивПечатныхФорм = Новый Массив;
	
	КоллекцияПечатныхФорм 	= Новый ТаблицаЗначений;
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяМакета");
	КоллекцияПечатныхФорм.Колонки.Добавить("ТабличныйДокумент");
	КоллекцияПечатныхФорм.Колонки.Добавить("СинонимМакета");
	КоллекцияПечатныхФорм.Колонки.Добавить("Картинка");	
	КоллекцияПечатныхФорм.Колонки.Добавить("ПолныйПутьКМакету");
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяФайлаПечатнойФормы");
	КоллекцияПечатныхФорм.Колонки.Добавить("ИмяВРЕГ");
	
	ПараметрыПечати 		= Новый Структура("ДополнитьКомплектВнешнимиПечатнымиФормами",Ложь);
	МассивОбъектов			= Новый Массив;
	МассивОбъектов.Добавить(ОбъектПечати);
	Если ИмяФормыПечати = "ЖБИ_ДоговорЗаявка" Тогда
		НоваяСтр 				= КоллекцияПечатныхФорм.Добавить();
		НоваяСтр.ИмяВРЕГ		= ВРЕГ("ЖБИ_ДоговорЗаявка");
		НоваяСтр.ИмяМакета      = "ЖБИ_ДоговорЗаявка";
		СформироватьПечатныеФормыДоговорЗаявка(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм);
	ИначеЕсли ИмяФормыПечати = "ЖБИ_Доверенность" Тогда
		НоваяСтр 				= КоллекцияПечатныхФорм.Добавить();
		НоваяСтр.ИмяВРЕГ		= ВРЕГ("ЖБИ_Доверенность");
		НоваяСтр.ИмяМакета      = ИмяФормыПечати;		
		ПараметрыПечати.Вставить("Тип","ДоверенностьМ2");		
		СформироватьПечатныеФормыДоверенность(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм);
	ИначеЕсли ИмяФормыПечати = "ЖБИ_ТТН" Тогда
		НоваяСтр 				= КоллекцияПечатныхФорм.Добавить();
		НоваяСтр.ИмяВРЕГ		= ВРЕГ("ЖБИ_ТТН");
		НоваяСтр.ИмяМакета      = "ЖБИ_ТТН";				
		СформироватьПечатныеФормыТТН(ОбъектПечати,ПараметрыПечати,КоллекцияПечатныхФорм,МассивПечатныхФорм);
	Иначе 
		НоваяСтр 				= КоллекцияПечатныхФорм.Добавить();
		НоваяСтр.ИмяВРЕГ		= ВРЕГ(ИмяФормыПечати);
		НоваяСтр.ИмяМакета      = ИмяФормыПечати;
		Документы.ЖБИ_ФактическиеОтгрузки.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, Новый СписокЗначений, Новый Структура, МассивПечатныхФорм);
	КонецЕсли;

	Возврат МассивПечатныхФорм;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ЗаполнитьТоварыНаполнениеРейса(Ссылка) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТоварыНаполнениеРейса,
	"Ссылка",
	Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()
	
	ТекДанные = ЖБИ_ОбщийМодульКлиентСервер.ПроверитьТекСтрокуНаДоступность(ЭтаФорма,"Список");
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	ЗаполнитьТоварыНаполнениеРейса(ТекДанные.Ссылка);
		
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяОперации()
	
	ИмяОперации = "";
	ИмяСтраницы = Элементы.СтраницыОсновнаяФорма.ТекущаяСтраница.Имя;	
	Если ИмяСтраницы = "СтраницаДокументов" Тогда
		ИмяОперации = "СтраницаДокументов";
		Элементы.КомандаДокументы.Пометка = Истина;
	КонецЕсли;
	
	Возврат ИмяОперации;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПараметрыДинамическогоСписка()

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТоварыНаполнениеРейса,
	"Ссылка",
	ПредопределенноеЗначение("Документ.ЖБИ_ФактическиеОтгрузки.ПустаяСсылка"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоУмолчаниюДинамическогоСписка()
	
	//ОтборМенеджер = ТекущийПользователь;
	//УстановитьОтборДинамическогоСпискаПоЭлементу(Элементы.ОтборМенеджер, "Менеджер",Неопределено);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент,ИмяОтбора,ВидСравнения)
	
	Если ЗначениеЗаполнено(ЭтотОбъект[Элемент.Имя]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, ИмяОтбора, ЭтотОбъект[Элемент.Имя], ВидСравнения, , );
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список,ИмяОтбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинСпискаПоСегменту(СписокСегмент, ИмяОтбора, ВидСравнения)	
	ЖБИ_ОбщийМодульОтчетыИОбработки.УстановитьОтборДинСпискаПоСегменту(СписокСегмент,Список,ИмяОтбора,ВидСравнения);	
КонецПроцедуры

#КонецОбласти

#Область ПРОЦЕДУРЫ_ОБРАБОТЧИКИ_ЭЛЕМЕНТОВ_УПРАВЛЕНИЯ_ФОРМЫ

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого Строка Из Строки Цикл
		Данные 	= Строка.Значение.Данные;
		Если НЕ ЗначениеЗаполнено(Данные.ДоговорЗаявки) Тогда 
			Продолжить;
		КонецЕсли;			
		Данные.ДоговорЗаявкаПредставдение 	= СокрЛП(Данные.ДоговорПеревозчик.Номер) + "-" + СокрЛП(Данные.ДоговорЗаявки.НомерЗаявки);	
	КонецЦикла;
	
КонецПроцедуры

#Область Команды

&НаКлиенте
Процедура КомандаИзменитьФактическиеОтгрузки(Команда)
	
	ТекДанные = ЖБИ_ОбщийМодульКлиентСервер.ПроверитьТекСтрокуНаДоступность(ЭтаФорма,"Список");
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЖБИ_ФактическиеОтгрузки.Форма.ФормаДокумента",
	Новый Структура("Ключ",ТекДанные.Ссылка),
	ЭтаФорма,,,,
	Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтаФорма, "КомандаИзменитьФактическиеОтгрузки"));
	
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаКлиенте
Процедура ОтборМенеджерПриИзменении(Элемент)	
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Менеджер",Неопределено);	
КонецПроцедуры

&НаКлиенте
Процедура ПлановыйОтгрузкиОтборПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ФактическиеОтгрузкиОтборПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Организация",Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ВодительОтборПриИзменении(Элемент)
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Водитель",Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура АвторОтборПриИзменении(Элемент)
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Автор",Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПокупательОтборПриИзменении(Элемент)
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Покупатель",Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикОтборПриИзменении(Элемент)
	УстановитьОтборДинамическогоСпискаПоЭлементу(Элемент, "Поставщик",Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ОтборСегментПриИзменении(Элемент)
	УстановитьОтборДинСпискаПоСегменту(ЭтотОбъект[Элемент.Имя], "Поставщик", ВидСравненияКомпоновкиДанных.ВСписке);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Список

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	КомандаИзменитьФактическиеОтгрузки("");		
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
КонецПроцедуры

#КонецОбласти


#Область ПРОЦЕДУРЫ_ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(ЭтаФорма.Параметры) = Тип("ДанныеФормыСтруктура") Тогда
		Для Каждого Реквизит Из ЭтаФорма.ПолучитьРеквизиты() Цикл
			Если ЭтаФорма.Параметры.Свойство(Реквизит.Имя) Тогда
				ЭтаФорма[Реквизит.Имя] = ЭтаФорма.Параметры[Реквизит.Имя];
			КонецЕсли;	
		КонецЦикла;	
	Иначе
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЖБИ_ОбщийМодульКлиентСервер.УстановитьКнопкиПечатиСервер(ПолучитьСписокПечатныхФорм(),ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	УстановитьДоступностьЭлементовФормы();
	Элементы.СтраницыОсновнаяФорма.ТекущаяСтраница = Элементы.СтраницаДокументов;
	ИмяОперации = ПолучитьИмяОперации();
	
	УстановитьПараметрыДинамическогоСписка();
	УстановитьОтборПоУмолчаниюДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	РезультатВыполнения = Новый Структура("СообщениеОбОшибке","");	
	Если Результат = КодВозвратаДиалога.Отмена Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда 
			
	КонецЕсли;	
	
	Если ДополнительныеПараметры = "КомандаИзменитьФактическиеОтгрузки" Тогда
		Элементы.Список.Обновить();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти