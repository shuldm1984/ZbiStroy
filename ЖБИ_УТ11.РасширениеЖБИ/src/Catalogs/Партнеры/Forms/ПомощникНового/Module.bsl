
&НаКлиенте
Процедура ЖБИ_ПриОткрытииПосле(Отказ)
	
	ЖБИ_ОбщийМодульСправочники.ЗаполнитьФормуОткрытия(ЭтаФорма);	
	ВидимостьСтраницПервичнаяИнформация();	
	Элементы.ВидЮрЛицо.Доступность = УказыватьЮридическиеРеквизиты;	
	
КонецПроцедуры

&НаКлиенте
&После("Далее")
Процедура ЖБИ_Далее(Команда)
	
	Если ПустаяСтрока(ИНН) Тогда
		ВывестиСообщениеПользователю(НСтр("ru = 'Не указан ИНН'"),"ИНН");
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПервичнаяИнформация;
		ЖБИ_ОбщийМодульСправочники.ЗаполнитьФормуОткрытия(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&После("Назад")
Процедура ЖБИ_Назад(Команда)
	ЖБИ_ОбщийМодульСправочники.ЗаполнитьФормуОткрытия(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
&После("ЗаполнитьРеквизитыКонтрагентаПоПолученнымДанным")
Процедура ЖБИ_ЗаполнитьРеквизитыКонтрагентаПоПолученнымДанным(РеквизитыКонтрагента)	
	ПолноеНаименование = "";	
КонецПроцедуры

&НаСервере
&После("ЗаписатьПартнераИСвязанныеДанные")
Процедура ЖБИ_ЗаписатьПартнераИСвязанныеДанные(СписокСозданных)
	
	//ЖБИ - Параметры ценообразования
	ТаблицаНабораЗаписей 		= РегистрыСведений.ЖБИ_ПараметрыЦенообразования.ПолучитьПустуюТаблицуНаборЗаписей();
	НоваяСтр 					= ТаблицаНабораЗаписей.Добавить();
	НоваяСтр.Партнер			= ПартнерВыбор;
	НоваяСтр.ПроцентНаценки 	= 22;
	НоваяСтр.ПовышающийПроцент 	= 26;
	НоваяСтр.ДопустимаяСкидка 	= 4;
	НоваяСтр.Автор 				= Пользователи.ТекущийПользователь();
	
	СообщениеОбОшибки			= "";
	СписокСвойствОбновления 	= "";
	Результат 					= РегистрыСведений.ЖБИ_ПараметрыЦенообразования.СоздатьОбновитьЗаписиРегистраСведений(ТаблицаНабораЗаписей,
									СписокСвойствОбновления,,СообщениеОбОшибки);									
	Если Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибки);										
	КонецЕсли;
	
	//Виды цен поставщиков
	СпрОбъектЦеныПоставщика 		 			= Справочники.ВидыЦенПоставщиков.СоздатьЭлемент();
	СпрОбъектЦеныПоставщика.Владелец 			= ПартнерВыбор;
	СпрОбъектЦеныПоставщика.Валюта   			= ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(СпрОбъектЦеныПоставщика.Валюта);
	СпрОбъектЦеныПоставщика.Наименование 		= "Цена с НДС";
	СпрОбъектЦеныПоставщика.ЦенаВключаетНДС     = Истина;
	СпрОбъектЦеныПоставщика.ДоступноДляЗакупки 	= Истина;
	Попытка
		СпрОбъектЦеныПоставщика.Записать();
	Исключение
		СообщениеОбОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибки);
	КонецПопытки;
	
КонецПроцедуры

