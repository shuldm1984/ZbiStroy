
#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ОБЩЕГО_НАЗНАЧЕНИЯ

&НаСервере
Процедура ПереопределитьЗапросСписка()
	
	ТекстЗапроса = "ВЫБРАТЬ
					|	РеестрДокументов.Ссылка КАК Ссылка,
					|	РеестрДокументов.НомерДокументаИБ КАК Номер,
					|	РеестрДокументов.ДатаДокументаИБ КАК Дата,
					|	РеестрДокументов.Статус КАК Статус,
					|	РеестрДокументов.Проведен КАК Проведен,
					|	РеестрДокументов.ПометкаУдаления КАК ПометкаУдаления,
					|	РеестрДокументов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
					|	РеестрДокументов.Партнер КАК Партнер,
					|	РеестрДокументов.Контрагент КАК Контрагент,
					|	РеестрДокументов.Договор КАК Договор,
					|	РеестрДокументов.Организация КАК Организация,
					|	РеестрДокументов.Подразделение КАК Подразделение,
					|	РеестрДокументов.МестоХранения КАК Склад,
					|	РеестрДокументов.Валюта КАК Валюта,
					|	РеестрДокументов.Сумма КАК СуммаДокумента,
					|	РеестрДокументов.Ответственный КАК Менеджер,
					|	РеестрДокументов.Комментарий КАК Комментарий,
					|	РеестрДокументов.НомерПервичногоДокумента КАК НомерВходящегоДокумента,
					|	РеестрДокументов.ДатаПервичногоДокумента КАК ДатаВходящегоДокумента,
					|	РеестрДокументов.Дополнительно КАК Дополнительно,
					|	СостоянияЭД.СостояниеВерсииЭД КАК СостояниеВерсииЭД,
					|	ВЫБОР
					|		КОГДА ДокументыСОшибкамиПроверкиКонтрагентов.Документ ЕСТЬ NULL
					|			ТОГДА ЛОЖЬ
					|		ИНАЧЕ ИСТИНА
					|	КОНЕЦ КАК ЕстьОшибкиПроверкиКонтрагентов,
					|	ВЫБОР
					|		КОГДА РеестрДокументов.Проведен
					|			ТОГДА 0
					|		КОГДА РеестрДокументов.ПометкаУдаления
					|			ТОГДА 1
					|		ИНАЧЕ 2
					|	КОНЕЦ КАК НестандартнаяКартинка,
					|	РеестрДокументов.ДополнительнаяЗапись КАК ДополнительнаяЗапись,
					|	РеестрДокументов.ТипСсылки КАК ТипСсылки,
					|	ТИПЗНАЧЕНИЯ(РеестрДокументов.Ссылка) КАК ТипЗначения,
					|	&СтатусПроверки КАК СтатусПроверки,
					|	&ИндикаторПроверки КАК ИндикаторПроверки,
					|	ВЫБОР
					|		КОГДА ТИПЗНАЧЕНИЯ(РеестрДокументов.Ссылка) = ТИП(Документ.ЗаказКлиента)
					|			ТОГДА 
					|				ВЫРАЗИТЬ(РеестрДокументов.Ссылка КАК Документ.ЗаказКлиента).ЖБИ_НомерСпецификации
					|		ИНАЧЕ """"
					|	КОНЕЦ КАК НомерСпецификации,
					|	ВЫБОР
					|		КОГДА ТИПЗНАЧЕНИЯ(РеестрДокументов.Ссылка) = ТИП(Документ.ЗаказКлиента)
					|			ТОГДА 
					|				РеестрДокументов.Договор.Номер
					|		ИНАЧЕ """"
					|	КОНЕЦ КАК НомерДоговора
					|ИЗ
					|	(ВЫБРАТЬ
					|		ДанныеДокумента.Ссылка КАК Ссылка
					|	ИЗ
					|		РегистрСведений.РеестрДокументов КАК ДанныеДокумента
					|	ГДЕ
					|		ДанныеДокумента.Договор = &Договор
					|	
					|	ОБЪЕДИНИТЬ
					|	
					|	ВЫБРАТЬ
					|		ДанныеРасшифровкиПлатежа.Ссылка
					|	ИЗ
					|		(ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка КАК Ссылка
					|		ИЗ
					|			Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)
					|		
					|		ОБЪЕДИНИТЬ ВСЕ
					|		
					|		ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка
					|		ИЗ
					|			Документ.РасходныйКассовыйОрдер КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)
					|		
					|		ОБЪЕДИНИТЬ ВСЕ
					|		
					|		ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка
					|		ИЗ
					|			Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)
					|		
					|		ОБЪЕДИНИТЬ ВСЕ
					|		
					|		ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка
					|		ИЗ
					|			Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)
					|		
					|		ОБЪЕДИНИТЬ ВСЕ
					|		
					|		ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка
					|		ИЗ
					|			Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)
					|		
					|		ОБЪЕДИНИТЬ ВСЕ
					|		
					|		ВЫБРАТЬ
					|			ДанныеДокумента.Ссылка
					|		ИЗ
					|			Документ.ОперацияПоПлатежнойКарте КАК ДанныеДокумента
					|		ГДЕ
					|			ДанныеДокумента.Ссылка В
					|					(ВЫБРАТЬ
					|						ДанныеТабличнойЧасти.Ссылка
					|					ИЗ
					|						Документ.ОперацияПоПлатежнойКарте.РасшифровкаПлатежа КАК ДанныеТабличнойЧасти
					|					ГДЕ
					|						ДанныеТабличнойЧасти.Ссылка = ДанныеДокумента.Ссылка
					|						И ДанныеТабличнойЧасти.Заказ = &Договор)) КАК ДанныеРасшифровкиПлатежа) КАК ДокументыДоговора
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
					|		ПО (ДокументыДоговора.Ссылка = РеестрДокументов.Ссылка)
					|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЭД КАК СостоянияЭД
					|		ПО (СостоянияЭД.СсылкаНаОбъект = РеестрДокументов.Ссылка)
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыСОшибкамиПроверкиКонтрагентов КАК ДокументыСОшибкамиПроверкиКонтрагентов
					|		ПО (РеестрДокументов.Ссылка = ДокументыСОшибкамиПроверкиКонтрагентов.Документ)
					|			И (&ИспользованиеПроверкиВозможно)}
					|{ГДЕ
					|	РеестрДокументов.Ссылка.* КАК Ссылка,
					|	РеестрДокументов.МестоХранения.* КАК Склад,
					|	РеестрДокументов.Организация.* КАК Организация,
					|	РеестрДокументов.ДатаДокументаИБ КАК Дата,
					|	РеестрДокументов.НомерДокументаИБ КАК Номер,
					|	РеестрДокументов.Статус КАК Статус,
					|	РеестрДокументов.ТипСсылки КАК ТипСсылки,
					|	РеестрДокументов.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
					|	РеестрДокументов.Партнер.* КАК Партнер,
					|	РеестрДокументов.Контрагент.* КАК Контрагент,
					|	РеестрДокументов.Договор.* КАК Договор,
					|	РеестрДокументов.Подразделение.* КАК Подразделение,
					|	РеестрДокументов.Ответственный.* КАК Менеджер,
					|	РеестрДокументов.Дополнительно КАК Дополнительно,
					|	РеестрДокументов.Комментарий КАК Комментарий,
					|	РеестрДокументов.Проведен КАК Проведен,
					|	РеестрДокументов.ПометкаУдаления КАК ПометкаУдаления,
					|	((РеестрДокументов.ДатаДокументаИБ >= &НачалоПериода
					|			ИЛИ &НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
					|			И (РеестрДокументов.ДатаДокументаИБ <= &КонецПериода
					|				ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))) КАК Поле2}";	

	СвойстваСписка 				= ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	//СвойстваСписка.ОсновнаяТаблица = "Справочник.Номенклатура";
	//СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокДокументов,СвойстваСписка);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкиДС()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	// Опишем ревизиты формы
	ЖБИ_Данные = Новый РеквизитФормы("НомерСпецификации", Новый ОписаниеТипов("Строка", , , Новый КвалификаторыСтроки(50)), , "№ спецификации");
	ДобавляемыеРеквизиты.Добавить(ЖБИ_Данные);	
	// Добавим новые реквизиты в форму
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	ЭлементФормы				= Элементы.Добавить("НомерСпецификации",Тип("ПолеФормы"),Элементы.СписокДокументов);
    ЭлементФормы.Вид 			= ВидПоляФормы.ПолеНадписи;
    ЭлементФормы.ПутьКДанным	= "СписокДокументов.НомерСпецификации";
	
КонецПроцедуры

#КонецОбласти

#Область ПРОЦЕДУРЫ_ОБРАБОТЧИКИ_ЭЛЕМЕНТОВ_УПРАВЛЕНИЯ_ФОРМЫ


#КонецОбласти

#Область ПРОЦЕДУРЫ_ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаСервере
&После("ПриСозданииНаСервере")
Процедура ЖБИ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.Договор) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Возврат;
	КонецЕсли;
	
	//ДоступностьРоли = ЖБИ_ОбщийМодульДокументыСервер.ОпределитьДоступностьРоли("ЖБИ_ОсновнаяРоль");
	//Если НЕ ЖБИ_ОбщийМодульДокументыСервер.ОпределитьДоступностьРоли("ЖБИ_ОсновнаяРоль") Тогда 
	//	Возврат;
	//КонецЕсли;
	
	ПереопределитьЗапросСписка();
	ДобавитьКолонкиДС();
	
КонецПроцедуры

#КонецОбласти
